buildscript {
    ext {
        kotlinVersion = '1.0.2'
        assertjVersion = '3.3.0'
        hsqlVersion = '2.3.3'
        hikariVersion = '2.4.3'
        junitVersion = '4.12'
    }
    repositories {
        jcenter()
        maven {
            url "https://dl.bintray.com/kotlin/kotlin-eap/"
        }
    }
    dependencies {
        classpath(
                "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion",
                'com.bmuschko:gradle-nexus-plugin:2.3.1',
                'net.researchgate:gradle-release:2.3.4',
                'com.github.ben-manes:gradle-versions-plugin:0.11.3',
        )
    }
}

allprojects {
    group = 'com.github.andrewoma.testczar'
    apply plugin: 'com.github.ben-manes.versions'
}

subprojects {
    apply plugin: 'maven'
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'jacoco'
    apply from: '../publish.gradle'

    repositories {
        maven {
            url "https://dl.bintray.com/kotlin/kotlin-eap/"
        }
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        compile(
                "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion",
                "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion",
        )
        testCompile(
                "junit:junit:$junitVersion",
                "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion",
                "org.assertj:assertj-core:$assertjVersion",
        )
    }

    compileKotlin {
        kotlinOptions.moduleName = "${project.group}.${project.name}"
    }

    test {
        exclude '**/Abstract*'
        exclude '**/*Unit*'
    }
}

project(':core') {
    dependencies {
        compile(
                "junit:junit:$junitVersion",
                "org.hsqldb:hsqldb:$hsqlVersion",
                "com.zaxxer:HikariCP:$hikariVersion",
        )
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "2.9"
}
